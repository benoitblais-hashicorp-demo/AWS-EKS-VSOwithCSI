# Copyright IBM Corp. 2024, 2026

resource "kubernetes_namespace_v1" "simple_app" {
  count      = var.step_2 ? 1 : 0
  depends_on = [time_sleep.step_2]
  metadata {
    name = "simple-app"
    labels = {
      "pod-security.kubernetes.io/enforce" = "privileged"
      "pod-security.kubernetes.io/audit"   = "privileged"
      "pod-security.kubernetes.io/warn"    = "privileged"
    }
  }
}

resource "kubernetes_namespace_v1" "ingress_nginx" {
  count      = var.step_2 ? 1 : 0
  depends_on = [time_sleep.step_2]
  metadata {
    name = "ingress-nginx"
    labels = {
      "pod-security.kubernetes.io/enforce" = "privileged"
      "pod-security.kubernetes.io/audit"   = "privileged"
      "pod-security.kubernetes.io/warn"    = "privileged"
    }
  }
}

resource "aws_eip" "nginx_ingress" {
  count = var.step_2 ? 3 : 0
  depends_on = [
    time_sleep.step_2,
    kubernetes_namespace_v1.simple_app,
  ]
}

resource "time_sleep" "eip_wait" {
  count = var.step_2 ? 1 : 0
  depends_on = [
    time_sleep.step_2,
    aws_eip.nginx_ingress,
  ]
  destroy_duration = "60s"
}

resource "helm_release" "nginx_ingress" {
  count = var.step_2 ? 1 : 0
  depends_on = [
    time_sleep.step_2,
    time_sleep.eip_wait,
    kubernetes_namespace_v1.ingress_nginx,
  ]
  name            = "ingress-nginx"
  repository      = "https://kubernetes.github.io/ingress-nginx"
  chart           = "ingress-nginx"
  namespace       = kubernetes_namespace_v1.ingress_nginx[0].metadata.0.name
  upgrade_install = true
  values = [<<-EOT
controller:
  service:
    ${var.public_hosted_zone != "" ? "targetPorts:\n      https: http" : ""}
    annotations:
      service.beta.kubernetes.io/aws-load-balancer-backend-protocol: tcp
      ${var.public_hosted_zone != "" ? format("service.beta.kubernetes.io/aws-load-balancer-ssl-cert: \"%s\"", try(aws_acm_certificate_validation.public[0].certificate_arn, "")) : "# TLS disabled"}
      ${var.public_hosted_zone != "" ? "service.beta.kubernetes.io/aws-load-balancer-ssl-ports: \"443\"" : ""}
      service.beta.kubernetes.io/aws-load-balancer-cross-zone-load-balancing-enabled: true
      service.beta.kubernetes.io/aws-load-balancer-eip-allocations: ${aws_eip.nginx_ingress[0].id},${aws_eip.nginx_ingress[1].id},${aws_eip.nginx_ingress[2].id}
      service.beta.kubernetes.io/aws-load-balancer-scheme: internet-facing
      service.beta.kubernetes.io/aws-load-balancer-type: nlb
    type: LoadBalancer
defaultBackend:
  enabled: true
EOT
  ]
}

resource "kubernetes_service_account_v1" "vault" {
  count      = var.step_2 ? 1 : 0
  depends_on = [time_sleep.step_2]
  metadata {
    name      = "vault-auth"
    namespace = kubernetes_namespace_v1.simple_app[0].metadata.0.name
  }
  automount_service_account_token = true
}

resource "kubernetes_secret_v1" "vault_token" {
  count      = var.step_2 ? 1 : 0
  depends_on = [time_sleep.step_2]
  metadata {
    name      = kubernetes_service_account_v1.vault[0].metadata.0.name
    namespace = kubernetes_namespace_v1.simple_app[0].metadata.0.name
    annotations = {
      "kubernetes.io/service-account.name" = "vault-auth"
    }
  }
  type                           = "kubernetes.io/service-account-token"
  wait_for_service_account_token = true
}

resource "kubernetes_cluster_role_binding_v1" "vault" {
  count      = var.step_2 ? 1 : 0
  depends_on = [time_sleep.step_2]
  metadata {
    name = "role-tokenreview-binding"
  }

  role_ref {
    api_group = "rbac.authorization.k8s.io"
    kind      = "ClusterRole"
    name      = "system:auth-delegator"
  }

  subject {
    kind      = "ServiceAccount"
    name      = kubernetes_service_account_v1.vault[0].metadata.0.name
    namespace = kubernetes_namespace_v1.simple_app[0].metadata.0.name
  }
}
